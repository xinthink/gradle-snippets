apply plugin: 'maven'
apply plugin: 'signing'

task jarSrc(type: Jar) {
    classifier = 'sources'
    baseName = "${project.name}"

    try { from sourceSets.main.groovy.srcDirs } catch (Exception e) {}
    try { from sourceSets.main.java.srcDirs } catch (Exception e) {}
    try { from sourceSets.main.resources.srcDirs } catch (Exception e) {}
}

task jarDoc(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = "${project.name}"
    from javadoc.destinationDir
}

artifacts {
    archives jarSrc
    archives jarDoc
}

signing {
    sign configurations.archives
}

uploadArchives {
    uploadDescriptor = true
    repositories.mavenDeployer {
        beforeDeployment { signing.signPom(it) }
        // repository(url: "file:///ws/repo")
        repository(url: isSnapshot ? sonatypeSnapshotRepo : sonatypeStageRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }
    }
}
